// SPDX-License-Identifier: MIT
pragma solidity ^0.8.13;

import {DefaultOperatorFilterer} from "../src/DefaultOperatorFilterer.sol";
import {BaseRegistryTest} from "./BaseRegistryTest.sol";
import {DefaultFilterer} from "./helpers/DefaultFilterer.sol";

import {OperatorFilterer} from "../src/OperatorFilterer.sol";
import {UpdatableFilterer} from "./helpers/UpdatableFilterer.sol";
import {OperatorFilterRegistryStub} from "./helpers/OperatorFilterRegistryStub.sol";

contract DefaultOperatorFiltererTest is BaseRegistryTest {
    DefaultFilterer filterer;
    address filteredAddress;
    address filteredCodeHashAddress;
    bytes32 filteredCodeHash;
    address notFiltered;
    address constant DEFAULT_SUBSCRIPTION = address(0x3cc6CddA760b79bAfa08dF41ECFA224f810dCeB6);

    function setUp() public override {
        super.setUp();
        notFiltered = makeAddr("not filtered");
        vm.startPrank(DEFAULT_SUBSCRIPTION);
        registry.register(DEFAULT_SUBSCRIPTION);

        filteredAddress = makeAddr("filtered address");
        registry.updateOperator(address(DEFAULT_SUBSCRIPTION), filteredAddress, true);
        filteredCodeHashAddress = makeAddr("filtered code hash");
        bytes memory code = hex"deadbeef";
        filteredCodeHash = keccak256(code);
        registry.updateCodeHash(address(DEFAULT_SUBSCRIPTION), filteredCodeHash, true);
        vm.etch(filteredCodeHashAddress, code);

        filterer = new DefaultFilterer();
        vm.stopPrank();
    }

    function testFilter() public {
        assertTrue(filterer.filterTest(notFiltered));
        vm.startPrank(filteredAddress);
        vm.expectRevert(abi.encodeWithSelector(AddressFiltered.selector, filteredAddress));
        filterer.filterTest(address(0x1));
        vm.stopPrank();
        vm.startPrank(filteredCodeHashAddress);
        vm.expectRevert(abi.encodeWithSelector(CodeHashFiltered.selector, filteredCodeHashAddress, filteredCodeHash));
        filterer.filterTest(address(0x1));
    }

    function testRevert_OperatorNotAllowed() public {
        address stubRegistry = address(new OperatorFilterRegistryStub());
        UpdatableFilterer updatableFilterer = new UpdatableFilterer(stubRegistry);
        vm.expectRevert(abi.encodeWithSelector(OperatorFilterer.OperatorNotAllowed.selector, address(filteredAddress)));
        updatableFilterer.checkFilterOperator(filteredAddress);
    }
}
